# vim: ft=python


Import('env')

import os


Help('''
        lowl            Build liblowl
        lowl-tests      Build and run tests for liblowl
        lowl-install    Install liblowl
'''.lstrip('\r\n'))


sources = [
    'lowl_sample.c',
    'lowl_hash.c',
    'lowl_math.c',
    'lowl_sketch.c',
    'lowl_types.c',
    'lowl_vectors.c',
]

lowl = env.SharedLibrary(target='lowl', source=sources, LIBS=['m'])
env.Alias('lowl', lowl)

tests = env.Program(target='tests', source=['tests.c'], LIBS=['lowl', 'm'],
    LIBPATH='.')
test_env = env.Clone()
test_env.Append(ENV = {env['LD_LIB_PATH_ENV_VAR']: lowl[0].dir.abspath})
run_tests = test_env.NoTargetLocalRunner('lowl-tests', [tests[0].abspath], [tests])

install_lowl = env.AlwaysBuild(env.Install('$INSTALL_PREFIX/lib', lowl))
env.Alias('lowl-install', install_lowl)

Return('lowl', 'install_lowl')
