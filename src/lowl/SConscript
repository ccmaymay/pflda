# vim: ft=python

Import('env')

import os

env.Append(CCFLAGS=['-O0', '-g', '-Wall', '-Wextra', '-std=gnu99'])

sources = [
    'lowl_sample.c',
    'lowl_hash.c',
    'lowl_math.c',
    'lowl_sketch.c',
    'lowl_types.c',
    'lowl_vectors.c',
]

lowl = env.SharedLibrary(target='lowl', source=sources, LIBS=['m'])
env.Alias('lowl', lowl)

tests = env.Program(target='tests', source=['tests.c'], LIBS=['lowl', 'm'],
    LIBPATH='.')
test_env = env.Clone()
test_env.Append(ENV = {'LD_LIBRARY_PATH': lowl[0].dir.abspath})
run_tests = test_env.NoTargetLocalRunner('tests', tests[0].abspath, [tests])
env.Alias('lowl-tests', run_tests)

install_lowl = env.AlwaysBuild(env.Install('$PREFIX/lib', lowl))
env.Alias('lowl-install', install_lowl)

Return('lowl')
